let AllJobsWithDuration = 
    DEH_Job_Logs_V2_CL
    | where CorrelationId != ""
    | where JobStatus_s == "Completed" and JobName_s in ("DimensionalModel", "DomainModel", "Controls")
    | join kind=inner (
        DEH_Job_Logs_V2_CL
        | where JobStatus_s == "Started" and JobName_s in ("DimensionalModel", "DomainModel", "Controls")
        | project JobId_g, JobStartTime = JobCompletionTime_s
        )
        on JobId_g
    | extend 
        JobStartTimeDT = todatetime(JobStartTime),
        JobCompletionTimeDT = todatetime(JobCompletionTime_s)
    | extend JobDuration = datetime_diff('second', JobCompletionTimeDT, JobStartTimeDT)
    | project
        AccountId_g,
        PurviewTenantId_g,
        CorrelationId,
        JobId_g,
        JobDuration, 
        JobStartTimeDT,
        JobCompletionTimeDT,
        JobName_s;
// Separate job types more efficiently
let ModelJobs = AllJobsWithDuration
    | where JobName_s in ("DimensionalModel", "DomainModel");
let ControlJobs = AllJobsWithDuration
    | where JobName_s == "Controls"
    | project CorrelationId;
let BYOCJobs = AllJobsWithDuration
    | where JobName_s == "StorageSync";
// Find Model jobs without Control jobs using single leftanti join
let ModelJobsWithoutControlJobs = ModelJobs 
    | join kind=leftanti ControlJobs on CorrelationId
    | join kind=inner BYOCJobs on CorrelationId
    | project
        CorrelationId,
        AccountId_g,
        PurviewTenantId_g,
        JobDuration,
        JobStartTimeDT,
        JobCompletionTimeDT;
// Union and summarize in single operation
ModelJobsWithoutControlJobs
| union (BYOCJobs
    | project
        CorrelationId,
        AccountId_g,
        PurviewTenantId_g,
        JobDuration,
        JobStartTimeDT,
        JobCompletionTimeDT)
| summarize 
    JobDuration = sum(JobDuration),
    JobStartTime = min(JobStartTimeDT),
    JobEndTime = max(JobCompletionTimeDT)
    by
    TenantId = PurviewTenantId_g,
    AccountId = AccountId_g, 
    MDQBatchId = CorrelationId,
    ProcessingTier = "Basic",
    DMSScope = "BYOC"
| extend JobDurationMinutes = round(JobDuration / 60.0, 0)
| extend JobDurationModuloMin =  JobDurationMinutes % 60
| extend JobDurationModuloHours = toint(JobDurationMinutes / 60)
| extend ProcessingUnits = round(todecimal(iff(toint(JobDurationModuloMin) == 0, 1, toint(JobDurationModuloMin))) / 60, 2) + JobDurationModuloHours
| join kind=leftanti (
    CBSBillingReceipts_CL 
    | where Status_s == 'Succeeded'
    | project EventId_g
    )
    on $left.MDQBatchId == $right.EventId_g 